ENTRY(m_main)
OUTPUT_FORMAT(elf64-x86-64)

PHDRS
{
    text    PT_LOAD    FLAGS((1 << 0) | (1 << 2)) ; /* xr */
    rodata  PT_LOAD    FLAGS((1 << 2)) ;            /* r */
    data    PT_LOAD    FLAGS((1 << 1) | (1 << 2)) ; /* wr */
    dynamic PT_DYNAMIC FLAGS((1 << 1) | (1 << 2)) ; /* Dynamic PHDR for relocations */
}

SECTIONS
{
. = 0xffffffff80000000;
    .text : {
        *(.text .text.*)
    } :text
    . += CONSTANT(MAXPAGESIZE);
    .rodata : {
        *(.rodata .rodata.*)
    } :rodata
    . += CONSTANT(MAXPAGESIZE);
    .data : {
        *(.data .data.*)
    } :data
    .dynamic : {
        *(.dynamic)
    } :data :dynamic
    .bss : {
        *(COMMON)
        *(.bss .bss.*)
    } :data
    /DISCARD/ : {
        *(.note .note.*)
    }
}
